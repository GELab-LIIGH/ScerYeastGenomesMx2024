#Configuration file with definde variables
configfile: "config.yaml"
#Import auxiliary functions
import pandas as pd
from aux import *
#Parse configfile
reference = config['conc_reference']
samples_SACE = config['sace']
samples_SAPA = config['sapa']
samples = samples_SACE + samples_SAPA
f2SAPA = config['forced2sapa']
coords = config["coords"]
shared = config["shared"]
bmodels = config["models"]

# extract SNPs that are in the Paradoxus genome
rule get_paradoxus_regions:
    input:
        vcf = "data/{sample}_CONC.g.vcf.gz"
    output:
        vcf_paradoxus = "data/vcfs/{sample}.onlyParadoxus-chr.g.vcf"
    shell:
        """
        module load vcftools/0.1.14
          vcftools --gzvcf {input.vcf} \
          --chr SAPA_YPS138_v1_chr_01 \
          --chr SAPA_YPS138_v1_chr_02 \
          --chr SAPA_YPS138_v1_chr_03 \
          --chr SAPA_YPS138_v1_chr_04 \
          --chr SAPA_YPS138_v1_chr_05 \
          --chr SAPA_YPS138_v1_chr_06 \
          --chr SAPA_YPS138_v1_chr_07 \
          --chr SAPA_YPS138_v1_chr_08 \
          --chr SAPA_YPS138_v1_chr_09 \
          --chr SAPA_YPS138_v1_chr_10 \
          --chr SAPA_YPS138_v1_chr_11 \
          --chr SAPA_YPS138_v1_chr_12 \
          --chr SAPA_YPS138_v1_chr_13 \
          --chr SAPA_YPS138_v1_chr_14 \
          --chr SAPA_YPS138_v1_chr_15 \
          --chr SAPA_YPS138_v1_chr_16 \
          --recode --recode-INFO-all \
          --out {output.vcf_paradoxus}
        mv {output.vcf_paradoxus}.recode.vcf {output.vcf_paradoxus}
        """
rule combine:
    input:
        "data/vcfs/Matrix_core.g.vcf.gz",
        rules.get_paradoxus_regions.output
    params:
        vcfs_sample = "--variant data/vcfs/{sample}.onlyParadoxus-chr.g.vcf",
        vcfs_core = ("--variant data/vcfs/Matrix_core.g.vcf.gz")
    output:
        temp("data/vcfs/{sample}/Matrix_{sample}_noGT_combinedgvcfs.g.vcf.gz"),
    shell:
        """
        module load gatk/4.1.6.0
        gatk --java-options "-Xmx24g -Djava.io.tmpdir=`pwd`/tmp/" CombineGVCFs -R {reference} \
        {params.vcfs_sample} {params.vcfs_core} -O {output} --tmp-dir `pwd`/tmp/
        """

rule index:
    input:
        "data/vcfs/{sample}/Matrix_{sample}_noGT_combinedgvcfs.g.vcf.gz"
    output:
        temp("data/vcfs/{sample}/Matrix_{sample}_noGT_combinedgvcfs.g.vcf.gz.csi")
    shell:
        """
        module load bcftools/1.9
        bcftools index {input} -o {output}
        """

#INTROS = unpack(get_introgressions(coords))

rule cut_vcfs:
    input:
        rules.combine.output,
        index = rules.index.output
    params:
        info = lambda w: "{0}:{1}-{2}".format(*get_introg_info("data/introgression_coordinates.txt",w.intro))
    output:
        cut_matrix = temp("data/vcfs/{sample}/Matrix_{sample}_{intro}_noGT_combinedgvcfs_cut.g.vcf")
    shell:
        """
        module load bcftools/1.9
        bcftools view -r {params.info} {input[0]} -o {output.cut_matrix}
        """

rule genotype:
    input:
        rules.cut_vcfs.output
    output:
        temp("data/vcfs/{sample}/Matrix_{intro}_combinedgvcfs_genotype.vcf")
    shell:
        """
        module load gatk/4.1.6.0
        gatk --java-options "-Xmx16g" GenotypeGVCFs \
          -R {reference} \
          -V {input[0]} \
          -O {output}
        """
rule select_SNPS:
    # discard other types of Variants that are not SNPs
    input:
        rules.genotype.output
    output:
        temp("data/vcfs/{sample}/Matrix_{intro}_combinegvcfs_genotype_onlySNPs.vcf")
    shell:
        """
        module load gatk/4.1.6.0
        gatk --java-options "-Xmx16g" SelectVariants \
          -R {reference} \
          -V {input} \
          --select-type-to-include SNP \
          -O {output}
        """
rule filter_low:
    input:
        rules.select_SNPS.output
    output:
        temp("data/vcfs/{sample}/Matrix_{intro}_combinegvcfs_genotype_onlySNPs_filterlow.vcf")
    shell:
        """
        module load gatk/4.1.6.0
        gatk --java-options "-Xmx16g" VariantFiltration \
            -R {reference} \
            -V {input} \
            -O {output[0]} \
            --filter-name "SNP_QD_filters" --filter-expression "QD<2.0" \
            --filter-name "SNP_MQ_filters" --filter-expression "MQ<40.0" \
            --filter-name "SNP_FS_filters" --filter-expression "FS>60.0" \
            --filter-name "SNP_SOR_filters" --filter-expression  "SOR>3.0" \
            --filter-name "SNP_MQRankSum_filters" --filter-expression "MQRankSum<-12.5" \
            --filter-name "SNP_ReadPosRankSum_filters" --filter-expression "ReadPosRankSum<-8.0"
        """
rule remove_filtered:
    input:
        rules.filter_low.output
    output:
        temp("data/vcfs/{sample}/Matrix_{intro}_combinegvcfs_genotype_onlySNPs_filterlow_filteredSNPs.vcf")
    params:
        # these are intermediate files
        to_recode = "data/vcfs/{sample}/Matrix_{intro}_combinegvcfs_genotype_onlySNPs_filterlow_filteredSNPs",
        recoded = "data/vcfs/{sample}/Matrix_{intro}_combinegvcfs_genotype_onlySNPs_filterlow_filteredSNPs.recode.vcf"
    shell:
        """
        module load vcftools/0.1.14
        vcftools --vcf {input} --remove-filtered-all --recode --out \
            {params.to_recode}
        mv {params.recoded} {output}
        """
rule missing_sites:
    input:
        rules.remove_filtered.output
    output:
        temp("data/vcfs/{sample}/Matrix_{intro}_combinegvcfs_genotype_onlySNPs_filterlow_filteredSNPs_missing10.vcf")
    params:
        to_recode = "data/vcfs/{sample}/Matrix_{intro}_combinegvcfs_genotype_onlySNPs_filterlow_filteredSNPs_missing10",
        recoded = "data/vcfs/{sample}/Matrix_{intro}_combinegvcfs_genotype_onlySNPs_filterlow_filteredSNPs_missing10.recode.vcf",
        percentage_missing = "0.9"
    shell:
        """
        module load vcftools/0.1.14
        vcftools --vcf {input} \
            --max-missing {params.percentage_missing} \
            --recode --out {params.to_recode}
        mv {params.recoded} {output}
        """
# keep only biallelic SNPs
rule keep_biallelic:
    input:
        rules.missing_sites.output
    output:
        "data/vcfs/{sample}/Matrix_{intro}_combinegvcfs_genotype_onlySNPs_filterlow_filteredSNPs_missing40_biallelic.vcf"
    shell:
        """
        module load bcftools/1.9
        bcftools view -M2 -v snps {input} >{output}
        """
#Convert to vcf
rule vcf2phylip:
    input:
        rules.keep_biallelic.output
    params:
        prefix = "data/phyl/{sample}/Matrix_{intro}_filteredSNP_10missing_biallelic"
    output:
        "data/phyl/{sample}/Matrix_{intro}_filteredSNP_10missing_biallelic.phy"
    shell:
        """
        python scripts/vcf2phylip_v28.py -i {input} --output-prefix {params.prefix}
        mv {params.prefix}.min4.phy {output}
        """

dictGenes = pd.read_csv(bmodels, sep=',').set_index("Gene")["Model"].to_dict()

#Make phylo tree
rule phylo_tree:
    input:
        rules.vcf2phylip.output
    output:
        best = "data/phyl/{sample}/Matrix_{intro}_filteredSNP_10missing_biallelic.phy.contree"
    params:
        model = lambda w: dictGenes[w.intro],
        LikelihoodRate = 1000,
        bootstrap = 1000
    threads: 4
    shell:
        """
        module load iq-tree/2.3.6
        iqtree2 -s {input} -m {params.model} -T {threads}  -bb {params.bootstrap} -alrt {params.LikelihoodRate}
        """
shared_df = pd.read_csv(shared, sep='\t')

rule all:
    input:
        expand("data/phyl/{sample}/Matrix_{intro}_filteredSNP_10missing_biallelic.phy.contree", zip,sample = shared_df["Id"],intro = shared_df["Gene"])

rule all1:
    input:
        expand("data/phyl/{sample}/Matrix_{intro}_filteredSNP_10missing_biallelic.phy", zip,sample = shared_df["Id"],intro = shared_df["Gene"])
